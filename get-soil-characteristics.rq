#+ endpoint: http://fuseki:3030/potato/query
#+ method: GET
#+ summary: "Given a particular GPS coordinate, this operation returns the soil characteristics at that location. Some locations do not provide a result, this might be caused by: the soil at that location falls within the 'Other' BOFEK2012 category (i.e. with code 999). Note that the values hold for the top layer of soil. Because the BOFEK2012 standard provides values for different depth that we did not yet take into account for this version of the FoodCube. For now, it only gives the upper layer of soil at a given location. The material code refers to table 4 of this document: http://library.wur.nl/WebQuery/wurpubs/311151. The BOFEK2012 information is retrieved from this source: http://www.wur.nl/nl/show/Bodemfysische-Eenhedenkaart-BOFEK2012.htm. Example GPS coordinate Lat: 52.42134450661928 | Lon: 5.461099727271467" 

PREFIX spatial: <http://jena.apache.org/spatial#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wkt: <http://www.opengis.net/ont/geosparql#>
PREFIX potato: <http://ontology.tno.nl/foodcube/potato/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?soilType ?densityValue ?materialValue ?organicValue ?lutumValue ?siltValue
{
    ?place spatial:intersectBox(?_lat ?_long ?_lat ?_long) .
    ?place a potato:Soil .
    ?place a ?soilType .
  	?soilType rdfs:subClassOf ?soilSuperType .
    ?soilSuperType rdfs:subClassOf potato:Soil .
    ?soilType owl:equivalentClass ?restrict .
    ?restrict owl:intersectionOf (?densityElem ?materialElem ?organicElem ?lutumElem ?siltElem) .
    ?densityElem owl:hasValue ?densityValue .
    ?materialElem owl:hasValue ?materialValue .
    ?organicElem owl:hasValue ?organicValue .
    ?lutumElem owl:hasValue ?lutumValue .
    ?siltElem owl:hasValue ?siltValue .
}
