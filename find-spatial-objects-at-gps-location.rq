#+ endpoint: http://fuseki:3030/potato/query
#+ method: GET
#+ summary: Retrieves all spatial objects that contain the GPS coordinate and find their URI and a human-readable label. In case the spatial object is a parcel, it returns the crop that is being grown on that parcel. If the spatial objects is soil, then it returns the name of the type of soil. For example, use lat=52.75707 and long=5.90386.

PREFIX spatial: <http://jena.apache.org/spatial#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wkt: <http://www.opengis.net/ont/geosparql#>
PREFIX potato: <http://ontology.tno.nl/foodcube/potato/>
PREFIX time: <http://www.w3.org/2006/time#>

SELECT ?text 
{ 
	?place spatial:intersectBox(?_lat ?_long ?_lat ?_long) . 
	OPTIONAL { ?place a potato:CropParcel . ?place potato:grows ?crop . ?crop potato:name ?name . 
    ?place potato:forPeriod ?period . ?period time:hasBeginning ?begin . ?begin time:inXSDDate ?date . 
    BIND (concat(str(year(?date)), " ", ?name) AS ?text)
  	}
	OPTIONAL { ?place a ?soil . FILTER (?soil != potato:Underground) . ?soil rdfs:subClassOf ?soilType . ?soilType rdfs:subClassOf potato:Underground .
	BIND (strafter(str(?soilType), str(potato:)) AS ?text)
	}
}